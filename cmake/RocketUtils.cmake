include(CMakeParseArguments)

function(RocketRemoveMatchesFromList)
    set(options)
    set(oneValueArgs)
    set(multiValueArgs MATCHES)
    cmake_parse_arguments(ROCKET_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    foreach (ROCKET_LIST ${ROCKET_PARSED_UNPARSED_ARGUMENTS})
        foreach (ROCKET_ENTRY ${${ROCKET_LIST}})
            foreach (ROCKET_MATCH ${ROCKET_PARSED_MATCHES})
                if (${ROCKET_ENTRY} MATCHES ${ROCKET_MATCH})
                    list(REMOVE_ITEM ${ROCKET_LIST} ${ROCKET_ENTRY})
                endif()
            endforeach()
        endforeach()
        set(${ROCKET_LIST} ${${ROCKET_LIST}} PARENT_SCOPE)
    endforeach()
endfunction()

function(RocketAddTestsFromSourceList)
    set(options)
    set(oneValueArgs PREFIX)
    set(multiValueArgs LIBS OPTIONS DEFINITIONS)

    cmake_parse_arguments(ROCKET_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    foreach (ROCKET_LIST ${ROCKET_PARSED_UNPARSED_ARGUMENTS})
        foreach (ROCKET_ENTRY ${${ROCKET_LIST}})
            if (${ROCKET_ENTRY} MATCHES ".*_test.cpp")
                string(REGEX REPLACE "^.*\/(.*)_test\.cpp$" "\\1" testName "${ROCKET_ENTRY}")
                set(testName "${ROCKET_PARSED_PREFIX}-${testName}-test")

                add_executable(${testName} ${ROCKET_ENTRY})
                target_compile_options(${testName} PRIVATE ${ROCKET_PARSED_OPTIONS})
                target_compile_definitions(${testName} PRIVATE ${ROCKET_PARSED_DEFINITIONS})
                target_link_libraries(${testName} PRIVATE ${ROCKET_PARSED_LIBS})

                add_test(${testName} ${testName})
            endif()
        endforeach()
    endforeach()
endfunction()

function(RocketAddBenchmarksFromSourceList)
    set(options)
    set(oneValueArgs PREFIX)
    set(multiValueArgs LIBS COMPILE_OPTIONS LINK_OPTIONS DEFINITIONS)

    cmake_parse_arguments(ROCKET_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    foreach (ROCKET_LIST ${ROCKET_PARSED_UNPARSED_ARGUMENTS})
        foreach (ROCKET_ENTRY ${${ROCKET_LIST}})
            if (${ROCKET_ENTRY} MATCHES ".*_bm.cpp")
                string(REGEX REPLACE "^.*\/(.*)_bm\.cpp$" "\\1" benchmarkName "${ROCKET_ENTRY}")
                set(benchmarkName "${ROCKET_PARSED_PREFIX}-${benchmarkName}-bm")

                add_executable(${benchmarkName} ${ROCKET_ENTRY})
                target_compile_options(${benchmarkName} PRIVATE ${ROCKET_PARSED_COMPILE_OPTIONS})
                target_compile_definitions(${benchmarkName} PRIVATE ${ROCKET_PARSED_DEFINITIONS})
                target_link_options(${benchmarkName} PRIVATE ${ROCKET_PARSED_LINK_OPTIONS})
                target_link_libraries(${benchmarkName} PRIVATE ${ROCKET_PARSED_LIBS})
            endif()
        endforeach()
    endforeach()
endfunction()

function(RocketExcludeTestsAndBenchmarksFromSourceList ROCKET_LIST)
    list(FILTER ${ROCKET_LIST} EXCLUDE REGEX ".*_test.cpp")
    list(FILTER ${ROCKET_LIST} EXCLUDE REGEX ".*_bm.cpp")
    set(${ROCKET_LIST} ${${ROCKET_LIST}} PARENT_SCOPE)
endfunction()
